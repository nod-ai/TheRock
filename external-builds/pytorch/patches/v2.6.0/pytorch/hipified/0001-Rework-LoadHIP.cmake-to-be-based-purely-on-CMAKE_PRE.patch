From 2e234ce66e8b0d20096adeccd0298d9353238747 Mon Sep 17 00:00:00 2001
From: Stella Laurenzo <stellaraccident@gmail.com>
Date: Mon, 17 Feb 2025 16:47:57 -0800
Subject: [PATCH] Rework LoadHIP.cmake to be based purely on CMAKE_PREFIX_PATH.

* Eliminates dependence on `/opt/rocm` and path based heuristics.
* Normalizes package finding for Rocm 6.5+ layout.
---
 cmake/public/LoadHIP.cmake | 302 ++++++++++++++-----------------------
 1 file changed, 110 insertions(+), 192 deletions(-)

diff --git a/cmake/public/LoadHIP.cmake b/cmake/public/LoadHIP.cmake
index 3eb34b0..c0a69db 100644
--- a/cmake/public/LoadHIP.cmake
+++ b/cmake/public/LoadHIP.cmake
@@ -1,199 +1,117 @@
-set(PYTORCH_FOUND_HIP FALSE)
-
-# If ROCM_PATH is set, assume intention is to compile with
-# ROCm support and error out if the ROCM_PATH does not exist.
-# Else ROCM_PATH does not exist, assume a default of /opt/rocm
-# In the latter case, if /opt/rocm does not exist emit status
-# message and return.
-if(DEFINED ENV{ROCM_PATH})
-  set(ROCM_PATH $ENV{ROCM_PATH})
-  if(NOT EXISTS ${ROCM_PATH})
-    message(FATAL_ERROR
-      "ROCM_PATH environment variable is set to ${ROCM_PATH} but does not exist.\n"
-      "Set a valid ROCM_PATH or unset ROCM_PATH environment variable to fix.")
-  endif()
-else()
-  if(UNIX)
-    set(ROCM_PATH /opt/rocm)
-  else() # Win32
-    set(ROCM_PATH C:/opt/rocm)
-  endif()
-  if(NOT EXISTS ${ROCM_PATH})
-    message(STATUS
-        "ROCM_PATH environment variable is not set and ${ROCM_PATH} does not exist.\n"
-        "Building without ROCm support.")
-    return()
-  endif()
-endif()
-
-if(NOT DEFINED ENV{ROCM_INCLUDE_DIRS})
-  set(ROCM_INCLUDE_DIRS ${ROCM_PATH}/include)
-else()
-  set(ROCM_INCLUDE_DIRS $ENV{ROCM_INCLUDE_DIRS})
-endif()
-
-# MAGMA_HOME
-if(NOT DEFINED ENV{MAGMA_HOME})
-  set(MAGMA_HOME ${ROCM_PATH}/magma)
-  set(ENV{MAGMA_HOME} ${ROCM_PATH}/magma)
-else()
-  set(MAGMA_HOME $ENV{MAGMA_HOME})
-endif()
-
-# MIOpen isn't a part of HIP-SDK for Windows and hence, may have a different
-# installation directory.
-if(WIN32)
-  if(NOT DEFINED ENV{MIOPEN_PATH})
-    set(miopen_DIR C:/opt/miopen/lib/cmake/miopen)
-  else()
-    set(miopen_DIR $ENV{MIOPEN_PATH}/lib/cmake/miopen)
-  endif()
-endif()
-
-torch_hip_get_arch_list(PYTORCH_ROCM_ARCH)
-if(PYTORCH_ROCM_ARCH STREQUAL "")
-  message(FATAL_ERROR "No GPU arch specified for ROCm build. Please use PYTORCH_ROCM_ARCH environment variable to specify GPU archs to build for.")
-endif()
-message("Building PyTorch for GPU arch: ${PYTORCH_ROCM_ARCH}")
-
-# Add HIP to the CMAKE Module Path
-# needed because the find_package call to this module uses the Module mode search
-# https://cmake.org/cmake/help/latest/command/find_package.html#search-modes
-if(UNIX)
-  set(CMAKE_MODULE_PATH ${ROCM_PATH}/lib/cmake/hip ${CMAKE_MODULE_PATH})
-else() # Win32
-  set(CMAKE_MODULE_PATH ${ROCM_PATH}/cmake/ ${CMAKE_MODULE_PATH})
-endif()
-
-# Add ROCM_PATH to CMAKE_PREFIX_PATH, needed because the find_package
-# call to individual ROCM components uses the Config mode search
-list(APPEND CMAKE_PREFIX_PATH ${ROCM_PATH})
-
-macro(find_package_and_print_version PACKAGE_NAME)
-  find_package("${PACKAGE_NAME}" ${ARGN})
-  message("${PACKAGE_NAME} VERSION: ${${PACKAGE_NAME}_VERSION}")
-endmacro()
-
-# Find the HIP Package
-# MODULE argument is added for clarity that CMake is searching
-# for FindHIP.cmake in Module mode
-find_package_and_print_version(HIP 1.0 MODULE)
-
-if(HIP_FOUND)
-  set(PYTORCH_FOUND_HIP TRUE)
-
-  # Find ROCM version for checks
-  if(UNIX)
-    set(ROCM_VERSION_HEADER_PATH ${ROCM_INCLUDE_DIRS}/rocm-core/rocm_version.h)
-  else()
-    set(ROCM_VERSION_HEADER_PATH ${ROCM_INCLUDE_DIRS}/hip/hip_version.h)
-  endif()
-  get_filename_component(ROCM_HEADER_NAME ${ROCM_VERSION_HEADER_PATH} NAME)
-
-  if(EXISTS ${ROCM_VERSION_HEADER_PATH})
-    set(ROCM_HEADER_FILE ${ROCM_VERSION_HEADER_PATH})
-  else()
-    message(FATAL_ERROR "********************* ${ROCM_HEADER_NAME} could not be found ******************\n")
-  endif()
-
-  # Read the ROCM headerfile into a variable
-  file(READ ${ROCM_HEADER_FILE} ROCM_HEADER_CONTENT)
-
-  # Since Windows currently supports only a part of ROCm and names it HIP-SDK,
-  # we need to refer to the HIP-SDK equivalents of entities existing in ROCm lib.
-  if(UNIX)
-    set(ROCM_LIB_NAME "ROCM")
-  else() # Win32
-    set(ROCM_LIB_NAME "HIP")
-  endif()
-  # Below we use a RegEx to find ROCM version numbers.
-  # Note that CMake does not support \s for blank space. That is
-  # why in the regular expressions below we have a blank space in
-  # the square brackets.
-  # There are three steps:
-  # 1. Match regular expression
-  # 2. Strip the non-numerical part of the string
-  # 3. Strip leading and trailing spaces
-
-  string(REGEX MATCH "${ROCM_LIB_NAME}_VERSION_MAJOR[ ]+[0-9]+" TEMP1 ${ROCM_HEADER_CONTENT})
-  string(REPLACE "${ROCM_LIB_NAME}_VERSION_MAJOR" "" TEMP2 ${TEMP1})
-  string(STRIP ${TEMP2} ROCM_VERSION_DEV_MAJOR)
-  string(REGEX MATCH "${ROCM_LIB_NAME}_VERSION_MINOR[ ]+[0-9]+" TEMP1 ${ROCM_HEADER_CONTENT})
-  string(REPLACE "${ROCM_LIB_NAME}_VERSION_MINOR" "" TEMP2 ${TEMP1})
-  string(STRIP ${TEMP2} ROCM_VERSION_DEV_MINOR)
-  string(REGEX MATCH "${ROCM_LIB_NAME}_VERSION_PATCH[ ]+[0-9]+" TEMP1 ${ROCM_HEADER_CONTENT})
-  string(REPLACE "${ROCM_LIB_NAME}_VERSION_PATCH" "" TEMP2 ${TEMP1})
-  string(STRIP ${TEMP2} ROCM_VERSION_DEV_PATCH)
+macro(pytorch_load_hip)
+  find_package(hip REQUIRED CONFIG)
+  message(STATUS "hip version: ${hip_VERSION}")
+  find_package(amd_comgr REQUIRED)
+  message(STATUS "amd_comgr version: ${amd_comgr_VERSION}")
+  find_package(rocrand REQUIRED)
+  message(STATUS "rocrand version: ${rocrand_VERSION}")
+  find_package(hiprand REQUIRED)
+  message(STATUS "hiprand version: ${hiprand_VERSION}")
+  find_package(rocblas REQUIRED)
+  message(STATUS "rocblas version: ${rocblas_VERSION}")
+  find_package(hipblas REQUIRED)
+  message(STATUS "hipblas_VERSION: ${hipblas_VERSION}")
+  find_package(miopen REQUIRED)
+  message(STATUS "miopen version: ${miopen_VERSION}")
+  find_package(hipfft REQUIRED)
+  message(STATUS "hipfft version: ${hipfft_VERSION}")
+  find_package(hipsparse REQUIRED)
+  message(STATUS "hipsparse version: ${hipsparse_VERSION}")
+  find_package(rocprim REQUIRED)
+  message(STATUS "rocprim version: ${rocprim_VERSION}")
+  find_package(hipcub REQUIRED)
+  message(STATUS "hipcub version: ${hipcub_VERSION}")
+  find_package(rocthrust REQUIRED)
+  message(STATUS "rocthrust version: ${rocthrust_VERSION}")
+  find_package(hipsolver REQUIRED)
+  message(STATUS "hipsolver versio: ${hipsolver_VERSION}")
+  find_package(hiprtc REQUIRED)
+  message(STATUS "hiprtc version: ${hiprtc_VERSION}")
+
+  # Original version made these UNIX-only.
+  find_package(rccl REQUIRED)
+  message(STATUS "rccl version: ${rccl_VERSION}")
+  find_package(hsa-runtime64 REQUIRED)
+  message(STATUS "hsa-runtime64 version: ${hsa-runtime64_VERSION}")
+  find_package(hipblaslt REQUIRED)
+  message(STATUS "hipblaslt version: ${hipblaslt_VERSION}")
+
+  # Extract ROCM version parts from the hip package version.
+  string(REPLACE "." ";" ROCM_VERSION_PARTS "${hip_VERSION}")
+  list(GET ROCM_VERSION_PARTS 0 ROCM_VERSION_DEV_MAJOR)
+  list(GET ROCM_VERSION_PARTS 1 ROCM_VERSION_DEV_MINOR)
+  list(GET ROCM_VERSION_PARTS 2 ROCM_VERSION_DEV_PATCH)
+  set(ROCM_VERSION "${ROCM_VERSION_DEV_MAJOR}.${ROCM_VERSION_DEV_MINOR}.${ROCM_VERSION_DEV_PATCH}")
+
+  message(STATUS "\n***** ROCm version: ****\n")
+  message(STATUS "  ROCM_VERSION: ${ROCM_VERSION}")
+  message(STATUS "  ROCM_VERSION_DEV_MAJOR: ${ROCM_VERSION_DEV_MAJOR}")
+  message(STATUS "  ROCM_VERSION_DEV_MINOR: ${ROCM_VERSION_DEV_MINOR}")
+  message(STATUS "  ROCM_VERSION_DEV_PATCH: ${ROCM_VERSION_DEV_PATCH}")
+  message(STATUS "  HIP_VERSION_MAJOR: ${ROCM_VERSION_DEV_MAJOR}")
+  message(STATUS "  HIP_VERSION_MINOR: ${ROCM_VERSION_DEV_MINOR}")
 
   # Create ROCM_VERSION_DEV_INT which is later used as a preprocessor macros
   set(ROCM_VERSION_DEV "${ROCM_VERSION_DEV_MAJOR}.${ROCM_VERSION_DEV_MINOR}.${ROCM_VERSION_DEV_PATCH}")
   math(EXPR ROCM_VERSION_DEV_INT "(${ROCM_VERSION_DEV_MAJOR}*10000) + (${ROCM_VERSION_DEV_MINOR}*100) + ${ROCM_VERSION_DEV_PATCH}")
-
-  message("\n***** ROCm version from ${ROCM_HEADER_NAME} ****\n")
-  message("ROCM_VERSION_DEV: ${ROCM_VERSION_DEV}")
-  message("ROCM_VERSION_DEV_MAJOR: ${ROCM_VERSION_DEV_MAJOR}")
-  message("ROCM_VERSION_DEV_MINOR: ${ROCM_VERSION_DEV_MINOR}")
-  message("ROCM_VERSION_DEV_PATCH: ${ROCM_VERSION_DEV_PATCH}")
-  message("ROCM_VERSION_DEV_INT:   ${ROCM_VERSION_DEV_INT}")
-
-  math(EXPR TORCH_HIP_VERSION "(${HIP_VERSION_MAJOR} * 100) + ${HIP_VERSION_MINOR}")
-  message("HIP_VERSION_MAJOR: ${HIP_VERSION_MAJOR}")
-  message("HIP_VERSION_MINOR: ${HIP_VERSION_MINOR}")
-  message("TORCH_HIP_VERSION: ${TORCH_HIP_VERSION}")
-
-  # Find ROCM components using Config mode
-  # These components will be searced for recursively in ${ROCM_PATH}
-  message("\n***** Library versions from cmake find_package *****\n")
-  find_package_and_print_version(hip REQUIRED CONFIG)
-  find_package_and_print_version(amd_comgr REQUIRED)
-  find_package_and_print_version(rocrand REQUIRED)
-  find_package_and_print_version(hiprand REQUIRED)
-  find_package_and_print_version(rocblas REQUIRED)
-  find_package_and_print_version(hipblas REQUIRED)
-  find_package_and_print_version(miopen REQUIRED)
-  find_package_and_print_version(hipfft REQUIRED)
-  find_package_and_print_version(hipsparse REQUIRED)
-  find_package_and_print_version(rocprim REQUIRED)
-  find_package_and_print_version(hipcub REQUIRED)
-  find_package_and_print_version(rocthrust REQUIRED)
-  find_package_and_print_version(hipsolver REQUIRED)
-  find_package_and_print_version(hiprtc REQUIRED)
-
-  if(UNIX)
-    find_package_and_print_version(rccl)
-    find_package_and_print_version(hsa-runtime64 REQUIRED)
-    find_package_and_print_version(hipblaslt REQUIRED)
-
-    # roctx is part of roctracer
-    find_library(ROCM_ROCTX_LIB roctx64 HINTS ${ROCM_PATH}/lib)
-
-    # check whether HIP declares new types
-    set(PROJECT_RANDOM_BINARY_DIR "${PROJECT_BINARY_DIR}")
-    set(file "${PROJECT_BINARY_DIR}/hip_new_types.cc")
-    file(WRITE ${file} ""
-      "#include <hip/library_types.h>\n"
-      "int main() {\n"
-      "    hipDataType baz = HIP_R_8F_E4M3_FNUZ;\n"
-      "    return 0;\n"
-      "}\n"
-      )
-
-    try_compile(hip_compile_result ${PROJECT_RANDOM_BINARY_DIR} ${file}
-      CMAKE_FLAGS "-DINCLUDE_DIRECTORIES=${ROCM_INCLUDE_DIRS}"
-      COMPILE_DEFINITIONS -D__HIP_PLATFORM_AMD__ -D__HIP_PLATFORM_HCC__
-      OUTPUT_VARIABLE hip_compile_output)
-
-    if(hip_compile_result)
-      set(HIP_NEW_TYPE_ENUMS ON)
-      #message("HIP is using new type enums: ${hip_compile_output}")
-      message("HIP is using new type enums")
-    else()
-      set(HIP_NEW_TYPE_ENUMS OFF)
-      #message("HIP is NOT using new type enums: ${hip_compile_output}")
-      message("HIP is NOT using new type enums")
+  math(EXPR TORCH_HIP_VERSION "(${ROCM_VERSION_DEV_MAJOR} * 100) + ${ROCM_VERSION_DEV_MINOR}")
+
+  message(STATUS "  ROCM_VERSION_DEV_INT:   ${ROCM_VERSION_DEV_INT}")
+  message(STATUS "  TORCH_HIP_VERSION: ${TORCH_HIP_VERSION}")
+
+  # Locate the ROCM_ROCTX_LIB that kineto depends on. This is either part of
+  # roctracer (deprecated) and located with find_library(roctx64) or it is
+  # part of rocprofiler-sdk (aka. rocprofiler v3) as the rocprofiler-sdk-tx
+  # library.
+  # TODO: This isn't quite right and needs to mate up with whether kineto
+  # depends on roctracer or rocprofiler-sdk. The coupling here is fragile and
+  # needs to be reworked.
+  find_package(rocprofiler-sdk-roctx)
+  if(rocprofiler-sdk-roctx_FOUND)
+    message(STATUS "rocprofiler-sdk-roctx version: ${rocprofiler-sdk-roctx_VERSION} found (will use instead of roctracer)")
+    set(ROCM_ROCTX_LIB rocprofiler-sdk-roctx::rocprofiler-sdk-roctx-shared-library)
+  else()
+    find_library(ROCM_ROCTX_LIB roctx64)
+    if(NOT ROCM_ROCTX_LIB)
+      cmake(WARNING "Neither rocprofiler-sdk nor libroctx64.so was found: This may result in errors if components rely on it")
     endif()
-  else() # Win32
-    # With HIP-SDK 6.2, HIP declares new enum types on Windows
-    set(HIP_NEW_TYPE_ENUMS ON)
   endif()
+
+  # PyTorch makes some use of hip_add_library and friends, which are only
+  # available in the legacy FindHIP.cmake finder module. This is bundled in the
+  # same CMAKE_PREFIX_PATH as is used for the regular packages, but is put in
+  # a different place on Linux vs Windows for reasons that are lost to time:
+  #   Linux: lib/cmake/hip/FindHIP.cmake
+  #   Windows: lib/cmake/FindHIP.cmake
+  # While we could ask the user to provide an explicit CMAKE_MODULE_PATH, we
+  # do some path munging in an attempt to make this legacy hiccup transparent
+  # to most. If this mechanism ever breaks, the fix is to configure explicitly
+  # with CMAKE_MODULE_PATH pointing at the directory in the ROCM SDK that
+  # contains FindHIP.cmake.
+  function(find_rocm_sdk_module_path)
+    set(hip_lib_dir "${hip_LIB_INSTALL_DIR}")
+    foreach(candidate_path "${hip_lib_dir}/cmake" "${hip_lib_dir}/cmake/hip")
+      if(EXISTS "${candidate_path}/FindHIP.cmake")
+        list(PREPEND CMAKE_MODULE_PATH "${candidate_path}")
+        message(STATUS "Legacy FindHIP.cmake module found in ${candidate_path}")
+        set(CMAKE_MODULE_PATH "${CMAKE_MODULE_PATH}" PARENT_SCOPE)
+        return()
+      endif()
+    endforeach()
+
+    message(STATUS "Could not locate legacy FindHIP.cmake: You may need to set CMAKE_MODULE_PATH explicitly to its location")
+  endfunction()
+  find_rocm_sdk_module_path()
+  find_package(HIP 1.0 MODULE REQUIRED)
+
+  set(HIP_NEW_TYPE_ENUMS ON)
+  set(PYTORCH_FOUND_HIP ON)
+endmacro()
+
+message(STATUS "___ROCM")
+set(PYTORCH_FOUND_HIP FALSE)
+set(HIP_PLATFORM "amd")
+find_package(hip CONFIG)
+if(hip_FOUND)
+  pytorch_load_hip()
 endif()
-- 
2.43.0

